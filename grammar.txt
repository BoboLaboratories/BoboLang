FILE -> IMPORTLIST FILEP eof

IMPORTLIST -> import EXPR_QID IMPORTLIST
IMPORTLIST -> epsilon

EXPR_QID -> id QIDP

QIDP -> . id QIDP
QIDP -> epsilon

FILEP -> private PRIVATEFILEP FILEP
FILEP -> NATIVEFUNDEF FILEP
FILEP -> PROGRAM_FUNDEF FILEP
FILEP -> PROGRAM_STAT FILEP
FILEP -> epsilon

PRIVATEFILEP -> STATVARDECL
PRIVATEFILEP -> NATIVEFUNDEF
PRIVATEFILEP -> PROGRAM_FUNDEF

NATIVEFUNDEF -> native FUNSIG

STATVARDECL -> const id ASSIGN
STATVARDECL -> var id STATVARDECLP

STATVARDECLP -> ASSIGN
STATVARDECLP -> epsilon

PROGRAM_STAT -> STATVARDECL
PROGRAM_STAT -> EXPR_QID QIDSTATP

ASSIGN -> = EXPR

QIDSTATP -> ASSIGN
QIDSTATP -> INVOKEARGS

EXPR -> EXPR_QID QIDEXPRP

QIDEXPRP -> INVOKEARGS
QIDEXPRP -> epsilon

INVOKEARGS -> ( INVOKEARGLIST )

INVOKEARGLIST -> EXPR INVOKEARGLISTP
INVOKEARGLIST -> epsilon

INVOKEARGLISTP -> , EXPR INVOKEARGLISTP
INVOKEARGLISTP -> epsilon

FUNSIG -> fun id ( FUNARGLIST )

PROGRAM_FUNDEF -> FUNSIG STATLIST

STATLIST -> { STATLISTP }

STATLISTP -> PROGRAM_STAT STATLISTP
STATLISTP -> epsilon

FUNARG -> const id
FUNARG -> id

FUNARGLIST -> FUNARG FUNARGLISTP
FUNARGLIST -> epsilon

FUNARGLISTP -> , FUNARG FUNARGLISTP
FUNARGLISTP -> ASSIGN FUNDEFARGLISTP
FUNARGLISTP -> epsilon

FUNDEFARGLISTP -> , FUNARG ASSIGN FUNDEFARGLISTP
FUNDEFARGLISTP -> epsilon